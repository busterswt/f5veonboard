# Load Balancer As A Service iApp Template for BIG-IQ
#
# This simple load-balancing application template uses OpenStack
# terminology to create a BIG-IP configuration.
#
# In order to avoid present and future versioning issues, this
# template has no dependence on cli scripts or Tcl packages.
#
# Furthermore, for compatibility with BIG-IQ, no presentation
# logic is used and all APL variables are insured with default
# values as defined in the arrays shown below, called var_defaults
# and table_defaults. Thus, the template will not abort merely
# because BIG-IQ fails to supply a given value. However, iApp
# implementation will still fail if BIG-IP rejects the resultant
# configuration. For example, the iApp will fail if HTTP_COOKIE
# persistence is chosen with TCP protocol optimization, since BIG-IP
# requires HTTP protocol optimization in order to offer cookie
# persistence.

set underscore [expr {[string first "_" $tmsh::app_name] - 1}]
set truncated_app_name [string range $tmsh::app_name 0 [expr {$underscore > 0 ? $underscore : "end"}]]

array set var_defaults {
    vip__state enabled
    vip__addr Error
    vip__mask 255.255.255.255
    vip__port 80
    vip__connection_limit -1
    vip__protocol http
    vip__persist http-cookie
    vip__cookie jsessionid
    vip__clientssl_profile none
    vip__serverssl_profile none
    vip__name $tmsh::app_name
    pool__name $tmsh::app_name
    pool__monitor_name $tmsh::app_name
    pool__lb_method round-robin
    pool__monitor none
    pool__monitor_interval 5
    pool__monitor_timeout 16
    pool__monitor_send_string ""
    pool__monitor_recv_string ""
    pool__monitor_url_path "/"
    pool__monitor_expected_codes "200"
}

array set table_defaults {
    pool__members {
        state enabled
        addr Error
        port 80
        connection_limit 0
        ratio 1
    }
}

# iRule for RPS Throttling
set throttle_irule {
when HTTP_REQUEST {
    set expiration_time 300
    set client_ip \[IP::client_addr\]
    set req_limit $::vip__connection_limit
    set curr_time \[clock seconds\]
    set timekey starttime
    set reqkey reqcount
    set request_count \[session lookup uie \$reqkey\]
    if { \$request_count eq "" } {
        set request_count 1
        session add uie \$reqkey \$request_count  \$expiration_time
        session add uie \$timekey \[expr {\$curr_time - 2}\] \[expr {\$expiration_time + 2}\]
    } else {
        set start_time \[session lookup uie \$timekey\]
        incr request_count
        session add uie \$reqkey \$request_count \$expiration_time
        set elapsed_time \[expr {\$curr_time - \$start_time}\]
        if {\$elapsed_time < 60} {
          set elapsed_time 60
        }
        set curr_rate \[expr {\$request_count /(\$elapsed_time/60)}\]
        if {\$curr_rate > \$req_limit} {
          HTTP::respond 503 throttled "Retry-After" 60
        }
    }
}}

# iRule from SOL7392: Overview of universal persistence
set persist_irule {
when HTTP_REQUEST {
    if { \[HTTP::cookie + str(cookiename)\] ne \"\" } {
        persist uie \[string tolower \[HTTP::cookie + $::vip__cookie\]\] 3600
    }
}
when HTTP_RESPONSE {
    if { \[HTTP::cookie $::vip__cookie\] ne \"\" } {
        persist add uie \[string tolower \[HTTP::cookie + $::vip__cookie\]\] 3600
    }
}}

array set pool_state {
    enabled        {session user-enabled  state user-up}
    disabled       {state user-down}
    force-disabled {state user-down}
    drain-disabled {session user-disabled state user-up}
}

array set monitor {
    none  {none}
    ping  {[tmsh_create ltm monitor gateway-icmp $::pool__monitor_name \
            interval $::pool__monitor_interval \
            timeout  $::pool__monitor_timeout \
          ]}
    tcp   {[tmsh_create ltm monitor tcp $::pool__monitor_name \
            interval $::pool__monitor_interval \
            timeout  $::pool__monitor_timeout \
            send   \"$::pool__monitor_send_string\" \
            recv   \"$::pool__monitor_recv_string\" \
          ]}
    http  {[tmsh_create ltm monitor http $::pool__monitor_name \
            interval $::pool__monitor_interval \
            timeout  $::pool__monitor_timeout \
            send   \"$::pool__monitor_send_string\" \
            recv   \"$::pool__monitor_recv_string\" \
          ]}
    https {[tmsh_create ltm monitor https $::pool__monitor_name \
            interval $::pool__monitor_interval \
            timeout  $::pool__monitor_timeout \
            send   \"$::pool__monitor_send_string\" \
            recv   \"$::pool__monitor_recv_string\" \
          ]}
}

array set persist {
    none        {none}
    source-ip   {replace-all-with \{[tmsh_create ltm persistence \
                 source-addr source_ip_$truncated_app_name]\}}
    http-cookie {replace-all-with \{[tmsh_create ltm persistence \
                 cookie http_cookie_$truncated_app_name]\}}
    app-cookie  {replace-all-with \{[tmsh_create ltm persistence \
                 universal app_cookie_$truncated_app_name \
                 rule [tmsh_create ltm rule app_cookie_$truncated_app_name \
                 [subst $persist_irule]]]\}}
}

array set profiles {
    tcp    {replace-all-with \{ \
               [tmsh_create ltm profile fastl4 $truncated_app_name] \
           \}}
    http   {replace-all-with \{ \
               [tmsh_create ltm profile http $truncated_app_name \
                 insert-xforwarded-for enabled] \
           \}}
    https  {replace-all-with \{ \
               [expr {$vip__clientssl_profile ne "none" \
               ? "$vip__clientssl_profile \{ context clientside \}" : "" }] \
               [expr {$vip__serverssl_profile ne "none" \
               ? "$vip__serverssl_profile \{ context serverside \}" : "" }] \
               [tmsh_create ltm profile http $truncated_app_name \
                 insert-xforwarded-for enabled] \
           \}}
}

# tmsh command wrapper that writes to scriptd log and returns object name
proc tmsh_create { args } {
    set args [join $args]
    puts "tmsh create $args"
    tmsh::create $args
    set p [lindex $args 1]
    if { $p eq "virtual" || $p eq "pool" || $p eq "rule" } {
        return [lindex $args 2]
    } else {
        return [lindex $args 3]
    }
}

# constructor that handles IPv6 and port number wildcards
proc destination { addr port } {

    # 0 and * represent wildcard port assignments in the GUI,
    # but TMSH requires the string 'any' to specify a wildcard
    if { $port eq "0" || $port eq "*" } {
        set port "any"
    }

    # decide on IPv4 or IPv6 delimiter
    set delimiter [expr {[string match "*:*:*" $addr] ? ".":":"}]
    return $addr$delimiter$port
}

# safely set any variables that do not exist
foreach var [array names var_defaults] {
    if { ![info exists ::$var] || [set ::$var] eq "" } {
        set ::$var [subst $var_defaults($var)]
    }
}

# safely set column values that do not exist
set do_ratio_lb 0
set members {}
foreach table [array names table_defaults] {
    if { [info exists ::$table] } {
        array set column_defaults $table_defaults($table)
        foreach row [set ::$table] {
            set params {}
            array unset column

            # extract the iApp table data
            foreach column_data [lrange [split [join $row] "\n"] 1 end-1] {
                set name [lindex $column_data 0]
                set column($name) [lindex $column_data 1]
            }

            # fill in any empty table values
            foreach name [array names column_defaults] {
                if { ![info exists column($name)] || $column($name) eq "" } {
                    set column($name) $column_defaults($name)
                }
                if { $name eq "state" } {
                    append params " $pool_state($column($name))"
                } elseif { $name ne "addr" && $name ne "port" } {
                    if { $name eq "connection_limit" && $column($name) eq "-1" } {
                        set column($name) 0
                    }
                    if { $name eq "ratio" && $column($name) ne "1" } {
                        set do_ratio_lb 1
                    }
                    append params " [string map {_ -} $name] $column($name)"
                }
            }
            append members \
                " [destination $column(addr) $column(port)] \\\{$params\\\}"
        }
    }
}

# convert to ratio load balancing if ratios are assigned in the pool
array set ratio_lb {
    round-robin ratio-member
    least-connections-member ratio-least-connections-member
}
if { $do_ratio_lb } {
    set lb_method $ratio_lb($::pool__lb_method)
} else {
    set lb_method $::pool__lb_method
}

# set monitor send and receive strings
if { $::pool__monitor_send_string eq "" } {
    set ::pool__monitor_send_string "GET $::pool__monitor_url_path HTTP/1.0"
}
if { $::pool__monitor_recv_string eq "" } {
    if { [string first "-" $::pool__monitor_expected_codes] >= 0 } {
        set ::pool__monitor_recv_string "HTTP/1.(0|1) \[$::pool__monitor_expected_codes\]"
    } else {
        set ::pool__monitor_recv_string "HTTP/1.(0|1) ([string map {\, \| " " ""} $::pool__monitor_expected_codes])"
    }
}

# build the pool and monitor
set pool [tmsh_create ltm pool $::pool__name \
    load-balancing-mode $lb_method \
    monitor [subst $monitor($::pool__monitor)] \
    members [expr {[llength $members] \
    ? "replace-all-with \{ [join $members] \}" : "none"}]]

# build the virtual
if { $::vip__state ne "delete" } {
    tmsh_create ltm virtual ${vip__name} \
    $::vip__state ip-protocol tcp snat automap \
    destination [destination $::vip__addr $::vip__port] \
    mask $::vip__mask \
    pool $pool \
    persist  [subst $persist($::vip__persist)] \
    profiles [subst $profiles($::vip__protocol)] \
    rules    [expr {$::vip__connection_limit > 0 \
    ? "\{ [tmsh_create ltm rule rps_throttle_$truncated_app_name [subst $throttle_irule]] \}" \
    : "none"}]
}